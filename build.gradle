buildscript {
    repositories {
        maven { url = 'https://maven.minecraftforge.net/' }
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '5.1.+', changing: true
        classpath 'com.github.jengelman.gradle.plugins:shadow:4.0.4'
    }
}

plugins {
    id "com.jfrog.artifactory" version "4.28.1"
}

repositories {
    maven {
        url 'https://maven.dynamx.fr/artifactory/ACsLibRepo'
    }

    flatDir {
        dir 'libs'
    }
}

apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'maven-publish'
apply plugin: "com.jfrog.artifactory"

version = "1.2.14-dev2"
group = "fr.aym.acsguis" // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = "ACsGuis"

compileJava.options.encoding = 'UTF-8'

sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = '1.8'

minecraft {
    mappings channel: 'stable', version: '39-1.12'
    runs {
        client {
            workingDirectory project.file('run')
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'debug'
        }
        server {
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'debug'
        }
    }
}

dependencies {
    minecraft 'net.minecraftforge:forge:1.12.2-14.23.5.2860'

    implementation "fr.aym:ACsLib:1.2.12"
    implementation ":Sqript+v1.2.6.2:"
    implementation ":slick:"

    shadow "fr.aym:ACsLib:1.2.12"
    shadow files('libs/slick.jar')
}

shadowJar {
    classifier = 'all'
    configurations = [project.configurations.shadow]
}

reobf {
    shadowJar {
    }
}

jar.finalizedBy('reobfJar')

ext {
    builtFile = ""
    builtFileSources = ""
}


task onBuild(type: Jar) {
    builtFile = archiveName.replaceAll(".jar", "-all.jar")
    builtFileSources = archiveName.replaceAll(".jar", "-sources.jar")
    project.logger.lifecycle("Just built file is ${builtFile}")
}

task outputNoTestJar(type: Jar) {
    destinationDir = new File("build/libs/fixed/")
    archiveName = builtFile
    project.logger.lifecycle("Copy from ${builtFile}")
    from(zipTree("build/libs/${builtFile}")) {
        exclude '/fr/nico/'
        exclude '/fr/aym/acsguis/test/'
        exclude '/assets/acsguis/css/dnx_debug.css'
        exclude '/assets/acsguis/css/font2.ttf'
    }
    //into(file(destinationDir))
}
task outputNoTestJarSources(type: Jar) {
    destinationDir = new File("build/libs/fixed/")
    archiveName = builtFileSources
    from(zipTree("build/libs/${builtFileSources}")) {
        exclude '/fr/nico/'
        exclude '/fr/aym/acsguis/test/'
        exclude '/assets/acsguis/css/dnx_debug.css'
        exclude '/assets/acsguis/css/font2.ttf'
    }
    //into(file(destinationDir))
}

tasks.reobfShadowJar.mustRunAfter shadowJar
tasks.onBuild.mustRunAfter reobfShadowJar
outputNoTestJar.dependsOn onBuild
outputNoTestJarSources.dependsOn onBuild

build {
    dependsOn reobfShadowJar
    dependsOn outputNoTestJar
    //dependsOn outputNoTestJarSources
}

artifactoryPublish.dependsOn build
jar.finalizedBy('reobfJar')

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact source : ("build/libs/fixed/${builtFile}"), extension: 'jar'
            //artifact source: ("build/libs/fixed/${builtFileSources}"), classifier: "sources", extension: 'jar'
        }
    }
}

artifactory {
    contextUrl = "${artifactory_contextUrl}"
    publish {
        repository {
            repoKey = 'ACsGuisRepo'
            username = "${artifactory_user}"
            password = "${artifactory_password}"
            maven = true
        }
        defaults {
            publications('mavenJava')
        }
    }
}